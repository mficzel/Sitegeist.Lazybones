prototype(Sitegeist.Lazybones:Picture) < prototype(Neos.Fusion:Component) {
    @styleguide {
        props {
            imageSource = Sitegeist.Kaleidoscope:DummyImageSource
            sources = Neos.Fusion:RawArray {
                1 = Neos.Fusion:RawArray {
                    srcset = '1x, 1.5x, 2x'
                    media = 'screen and (min-width: 1600px)'
                }
                2 = Neos.Fusion:RawArray {
                    srcset = '320w, 480w, 800w, 1000w'
                    sizes = '(max-width: 320px) 280px, (max-width: 480px) 440px, 800px'
                    media = 'screen and (max-width: 1599px)'
                }
                3 = Neos.Fusion:RawArray {
                    imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                        text = "im am here for printing"
                    }
                    media = 'print'
                }
            }
            alt = 'Elva dressed as a fairy'
        }
    }

    lazy = ${Configuration.setting('Sitegeist.Lazybones.enable')}
    lazyClassName = ${Configuration.setting('Sitegeist.Lazybones.className')}
    lazyWidth = ${Configuration.setting('Sitegeist.Lazybones.width')}

    imageSource = Sitegeist.Kaleidoscope:DummyImageSource
    srcset = null
    sizes = null
    sources = null
    alt = null
    title = null
    class = null

    renderer = Neos.Fusion:Case {

        lazy {
            condition = ${props.lazy}
            renderer = afx`
                <picture @if.has={props.imageSource} class={props.class ? props.class + ' ' + props.lazyClassName : props.lazyClassName }>
                    <Neos.Fusion:Collection collection={props.sources} itemName="source" @children="itemRenderer" @if.has={props.sources}>
                        <source
                            @context.subImageSource={source.imageSource ? source.imageSource : props.imageSource}
                            media={source.media}
                            data-srcset={source.srcset ? subImageSource.srcset(source.srcset) : subImageSource.src()}
                            data-sizes={source.sizes}
                            data-sizes.@process.join={Type.isArray(value) ? Array.join(value, ', ') : value}
                        />
                    </Neos.Fusion:Collection>
                    <Sitegeist.Lazybones:Image
                        lazy={props.lazy}
                        lazyClassName={props.lazyClassName}
                        lazyWidth={props.lazyWidth}

                        imageSource={props.imageSource}
                        sizes={props.sizes}
                        srcset={props.srcset}
                        alt={props.alt}
                        title={props.title}
                    />
                </picture>
            `
        }

        default {
            condition = true
            renderer = Sitegeist.Kaleidoscope:Picture {
                imageSource = ${props.imageSource}
                srcset = ${props.srcset}
                sizes = ${props.sizes}
                sources = ${props.sources}
                alt = ${props.alt}
                title = ${props.title}
                class = ${props.class}
            }
        }
    }
}
